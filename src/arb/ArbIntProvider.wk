#>
# Source Code for the Arbitrary Integer Library for the
# Programming Language Wake
#
# ArbIntProvider.wk
#
# Licensed under the MIT license
# See LICENSE.TXT for details
#
# Author: Nathan Fairhurst
# Revised By: Michael Fairhurst
#
##
module arb;

import arb.ArbInt;
import arb.ArbIntBitManipulator;

every ArbIntProvider is:

    provides
		ArbIntBitManipulator,
		ArbIntProvider <- { return this; },
		ArbInt <- ArbInt(?Int[], ArbIntBitManipulator, ArbIntProvider),
        ArbInt <- (Text) {
			var Int[] = [];
			var i = 0;
			var j = 0;
			var k = 0;
			var hexChars = '0123456789ABCDEF';

			var sign = 0;
			if (Text.substr(0,1) == '-'s) {
				sign = 1;
			}

			if (Text.substr(0+sign,2+sign) == '0x') {
				//String is in Hex

				Text = "0" + Text.substr(2+sign,Text.getSize());

				var words = ((Text.getSize()) / 8).ceil();	//FORCES 32 BIT
				for (i = 0; i < words; i+=1) {
					Int[].push(0);
				}

				Text = Text.toUpper();

				var index = nothing;

				j = 0;
				k = 0;
				for (i = Text.getSize() - 1; i >= 0; i-=1) {
					index = hexChars.indexOf(Text.substr(i,i+1));
					if index exists {
						Int[j] = Int[j] + (index << (k*4));
					} else {
						return ArbInt([0]) from this;					//Should throw an Error instead
					}
					k+=1;
					if (k == 8) {										//FORCES 32 BIT
						k = 0;
						j+=1;
					}
				}

				var Bool = false;
				if (sign == 1) {
					for (i = 0; i < Int[].getSize(); i += 1) {
						Int[i] = ~Int[i];
						if (!Bool) {
							if (Int[i] == -1) {
								Int[i] = 0;
							} else {
								Int[i] += 1;
								Bool = true;
							}
						}
					}
				}

				var ArbInt = ArbInt(Int[]) from this;
				//Doesn't remove padding

				return ArbInt;
			} else {
				return ArbInt([0]) from this;							//Should throw an Error instead
			}
        },
        ArbInt <- (Int) {
            return ArbInt([Int]) from this;
        };
